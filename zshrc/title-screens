# -*- shell-script -*-
# if you are at a zsh prompt, make your screen title your current directory
precmd_auto_title_screens ()
{
  local TITLE=${HOST/.amazon.com/}:${PWD:t}
  # 'screen' sets STY as well, so for users who override the TERM
  # environment variable, checking STY is nice
  setopt UNSET # Avoid errors from undefined STY for users with 'NOUNSET'
  if [[ $TERM == "screen" || -n $STY ]]; then
      echo -ne "\ek$TITLE\e\\"
  fi
  if [[ $TERM == "xterm" ]]; then
      echo -ne "\e]0;$TITLE\a"
  fi
  setopt LOCAL_OPTIONS # restore value of UNSET
}

# if you are running a command, make your screen title the command you're
# running
preexec_auto_title_screens ()
{
  local CMDS
  local CMD
  set -A CMDS $(echo $1)
  #Use first word from command line, but treat sudo and ssh specially
  if [[ $CMDS[1] == "sudo" ]]; then
      CMD="sudo $CMDS[2]"
  else
      CMD=$CMDS[1]
  fi
  setopt UNSET # Avoid errors from undefined STY for users with 'NOUNSET'
  if [[ $TERM == "screen" || -n "$STY" ]]; then
    echo -ne "\ek$CMD\e\\"
  fi
  if [[ $TERM == "xterm" ]]; then
    echo -ne "\e]0;$CMD\a"
  fi
  setopt LOCAL_OPTIONS # restore value of UNSET
}

preexec_functions+='preexec_auto_title_screens'
precmd_functions+='precmd_auto_title_screens'
